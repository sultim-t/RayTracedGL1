// Copyright (c) 2021 Sultim Tsyrendashiev
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#version 460

#define DESC_SET_FRAMEBUFFERS 0
#include "ShaderCommonGLSLFunc.h"

layout(local_size_x = 64) in;

layout(constant_id = 0) const uint isSourcePing = 0;
layout(constant_id = 1) const uint useSimpleSharp = 0;

layout(push_constant) uniform CasPush_BT
{
    uvec4 con0;
    uvec4 con1;
} push;

    // CAS impl
    #define A_GPU 1
    #define A_GLSL 1
    #include "CAS/ffx_a.h"

    // CAS should operate in linear space 
    AF3 loadInput(const ASU2 p) 
    {    
        if (isSourcePing != 0)
        {
            return texelFetch(framebufUpscaledPing_Sampler, p, 0).rgb;
        }
        else
        {
            return texelFetch(framebufUpscaledPong_Sampler, p, 0).rgb;
        }               
    }

    void storeOutput(const ASU2 p, const AF4 color)
    {
        if (isSourcePing != 0)
        {
            imageStore(framebufUpscaledPong, p, color);
        }
        else
        {
            imageStore(framebufUpscaledPing, p, color);
        }            
    }

    AF3 CasLoad(const ASU2 p) 
    {
        return loadInput(p);
    }
    void CasInput(inout AF1 r,inout AF1 g,inout AF1 b){}
    #include "CAS/ffx_cas.h"

void applyCas()
{
    const bool sharpenOnly = true;

    AU2 gxy=ARmp8x8(gl_LocalInvocationID.x)+AU2(gl_WorkGroupID.x<<4u,gl_WorkGroupID.y<<4u);

    AF4 c;
    CasFilter(c.r,c.g,c.b,gxy,push.con0,push.con1,sharpenOnly);
    storeOutput(ASU2(gxy), c);

    gxy.x+=8u;
    CasFilter(c.r,c.g,c.b,gxy,push.con0,push.con1,sharpenOnly);
    storeOutput(ASU2(gxy), c);

    gxy.y+=8u;
    CasFilter(c.r,c.g,c.b,gxy,push.con0,push.con1,sharpenOnly);
    storeOutput(ASU2(gxy), c);

    gxy.x-=8u;
    CasFilter(c.r,c.g,c.b,gxy,push.con0,push.con1,sharpenOnly);
    storeOutput(ASU2(gxy), c);
}

AF4 sharp(const ASU2 p)
{
    const float w = 1.0;

    const AF3 r = 
        loadInput(p             )  * (1 + 4 * w) +
        loadInput(p + ASU2( 0, 1)) * (-w) +
        loadInput(p + ASU2( 1, 0)) * (-w) +
        loadInput(p + ASU2( 0,-1)) * (-w) +
        loadInput(p + ASU2(-1, 0)) * (-w);

    return AF4(r, 0);
}

void applySimpleSharp()
{
    ASU2 gxy = ASU2(ARmp8x8(gl_LocalInvocationID.x) + AU2(gl_WorkGroupID.x << 4u, gl_WorkGroupID.y << 4u));
    storeOutput(gxy, sharp(gxy));

    gxy.x += 8;
    storeOutput(gxy, sharp(gxy));

    gxy.y += 8;
    storeOutput(gxy, sharp(gxy));

    gxy.x -= 8;
    storeOutput(gxy, sharp(gxy));
}

void main()
{
    if (useSimpleSharp != 0)
    {
        applySimpleSharp();
    }
    else 
    {
        applyCas();
    }
}