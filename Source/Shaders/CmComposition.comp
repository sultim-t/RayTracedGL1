// Copyright (c) 2021 Sultim Tsyrendashiev
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#version 460

#define DESC_SET_FRAMEBUFFERS 0
#define DESC_SET_GLOBAL_UNIFORM 1
#define DESC_SET_TONEMAPPING 2
#include "ShaderCommonGLSLFunc.h"

layout(local_size_x = COMPUTE_COMPOSE_GROUP_SIZE_X, local_size_y = COMPUTE_COMPOSE_GROUP_SIZE_Y, local_size_z = 1) in;

vec3 Uncharted2TonemapOp(vec3 x)
{
    float A = 0.15;
    float B = 0.50;
    float C = 0.10;
    float D = 0.20;
    float E = 0.02;
    float F = 0.30;

    return ((x*(A*x+C*B)+D*E)/(x*(A*x+B)+D*F))-E/F;
}

vec3 Uncharted2Tonemap(vec3 color)
{
    float w = globalUniform.luminanceWhitePoint;    
    return Uncharted2TonemapOp(2.0 * color) / Uncharted2TonemapOp(vec3(w));
}

vec3 reinhard(const vec3 c)
{
    const vec3 c1 = clamp(c, exp2(globalUniform.minLogLuminance), exp2(globalUniform.maxLogLuminance));
    const float w2 = globalUniform.luminanceWhitePoint * globalUniform.luminanceWhitePoint;
    return c * (1.0 + c1 / w2) / (1.0 + c1);
}

void main()
{
    const ivec2 pix = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);

    if (pix.x >= uint(globalUniform.renderWidth) || pix.y >= uint(globalUniform.renderHeight))
    {
        return;
    }


#ifdef DEBUG_SHOW_SH
    ivec2 checkSHRange = ivec2(800, 400);

    if (pix.x < checkSHRange.x && pix.y < checkSHRange.y)
    {
        vec2 uv = vec2(pix.x / float(checkSHRange.x), pix.y / float(checkSHRange.y));

        float theta = uv.x * 2.0 * M_PI;
        float phi = uv.y * M_PI;
        vec3 normal = vec3(cos(theta) * sin(phi), sin(theta) * sin(phi), cos(phi));

        ivec2 centerPix = ivec2(globalUniform.renderWidth * 0.5, globalUniform.renderHeight * 0.5);
        SH indirSH = texelFetchSH(
            //framebufUnfilteredIndirectSH_R_Sampler, framebufUnfilteredIndirectSH_G_Sampler, framebufUnfilteredIndirectSH_B_Sampler,
            framebufIndirPongSH_R_Sampler, framebufIndirPongSH_G_Sampler, framebufIndirPongSH_B_Sampler, 
            getCheckerboardPix(centerPix));

        imageStore(framebufPreFinal, pix, vec4(SHToIrradiance(indirSH, normal), 0));
        return;
    }
#endif


    vec3 color = imageLoad(framebufPreFinal, pix).rgb;

    const vec4 albedo4 = texelFetchAlbedo(getCheckerboardPix(pix));
    
    // if not sky
    if (globalUniform.areFramebufsInitedByRT != 0 && !isSky(albedo4))
    {
        color *= getExposure();
        //color = reinhard(color);
        color = Uncharted2Tonemap(color);

        const vec3 emission = albedo4.rgb * getScreenEmissionFromAlbedo4(albedo4) * globalUniform.emissionMaxScreenColor;
        color += emission;
    }
    else
    {
        const vec3 throughput = globalUniform.areFramebufsInitedByRT != 0 ? texelFetch(framebufThroughput_Sampler, getCheckerboardPix(pix), 0).rgb : vec3(1.0);

        color = albedo4.rgb * throughput;
    }



    if ((globalUniform.debugShowFlags & DEBUG_SHOW_FLAG_MOTION_VECTORS) != 0)
    {
        const vec2 m = texelFetch(framebufMotion_Sampler, getCheckerboardPix(pix), 0).rg;
        color.r = abs(m.r);
        color.g = abs(m.g);
        color.b = 0;
    }
    else if ((globalUniform.debugShowFlags & DEBUG_SHOW_FLAG_SECTORS) != 0)
    {
        uint sectorArrayIndex = texelFetch(framebufSectorIndex_Sampler, getCheckerboardPix(pix), 0).r; 
        if (sectorArrayIndex != SECTOR_INDEX_NONE)
        {
            const uint colorCount = 32;
            uint r = uint(sectorArrayIndex);
            color = vec3(
                0,
                ((r % (colorCount - 1)) + 1) / float(colorCount),
                (((r >> (uint(log2(colorCount)))) % (colorCount - 1)) + 1) / float(colorCount)
            );
        }
        else
        {
            color = vec3(1,0,0);
        }
    }
#if 0
    else if ((globalUniform.debugShowFlags & DEBUG_SHOW_FLAG_UNFILTERED_DIFF) != 0)
    {
        color = texelFetchUnfilteredDirect(getCheckerboardPix(pix));
    }
    else if ((globalUniform.debugShowFlags & DEBUG_SHOW_FLAG_UNFILTERED_SPEC) != 0)
    {
        color = texelFetchUnfilteredSpecular(getCheckerboardPix(pix));
    }
    else if ((globalUniform.debugShowFlags & DEBUG_SHOW_FLAG_UNFILTERED_INDIR) != 0)
    {
    }
#endif
#if GRADIENT_ESTIMATION_ENABLED
    else if ((globalUniform.debugShowFlags & DEBUG_SHOW_FLAG_GRADIENTS) != 0)
    {
        const vec4 gradDS = texelFetch(framebufDiffAndSpecPingGradient_Sampler, getCheckerboardPix(pix) / COMPUTE_ASVGF_STRATA_SIZE, 0);
        const vec4 gradI = texelFetch(framebufIndirPingGradient_Sampler, getCheckerboardPix(pix) / COMPUTE_ASVGF_STRATA_SIZE, 0);
        const float lambda = getAntilagAlpha(gradDS.r, gradDS.g);
        const float lambdaSpec = getAntilagAlpha(gradDS.b, gradDS.a);
        const float lambdaIndir = clamp(gradI.r, 0.0, 1.0);
        color.rgb = vec3(lambda, lambdaSpec, lambdaIndir);
    }
#endif

   imageStore(framebufPreFinal, pix, vec4(color, 0));
}