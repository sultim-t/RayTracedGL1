// Copyright (c) 2021 Sultim Tsyrendashiev
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#version 460

#extension GL_EXT_control_flow_attributes : require

#define DESC_SET_FRAMEBUFFERS 0
#define DESC_SET_GLOBAL_UNIFORM 1
#define DESC_SET_TONEMAPPING 2
#define TONEMAPPING_BUFFER_WRITEABLE
#include "ShaderCommonGLSLFunc.h"

layout(local_size_x = COMPUTE_LUM_HISTOGRAM_BIN_COUNT, local_size_y = 1, local_size_z = 1) in;

shared uint histogramWorkGroup[COMPUTE_LUM_HISTOGRAM_BIN_COUNT];

// https://bruop.github.io/exposure/
// http://www.alextardif.com/HistogramLuminance.html
// https://knarkowicz.wordpress.com/2016/01/09/automatic-exposure/

void main()
{
    uint localBinIndex = uint(gl_LocalInvocationIndex);

    uint countInLocalBin = tonemapping.histogram[localBinIndex];
    histogramWorkGroup[localBinIndex] = countInLocalBin * localBinIndex;

    groupMemoryBarrier();

    // for the next frame
    tonemapping.histogram[localBinIndex] = 0;

    [[unroll]]
    for (uint cutoff = (COMPUTE_LUM_HISTOGRAM_BIN_COUNT >> 1); cutoff > 0; cutoff >>= 1)
    {
        if (localBinIndex < cutoff)
        {
            histogramWorkGroup[localBinIndex] += histogramWorkGroup[localBinIndex + cutoff];
        }

        groupMemoryBarrier();
    }

    // only one invocation should write the result
    if (localBinIndex == 0)
    {
        if (globalUniform.stopEyeAdaptation != 0)
        {
            return;
        }

        float minLogLuminance = globalUniform.minLogLuminance;
        float maxLogLuminance = globalUniform.maxLogLuminance;
        float logLuminanceRange = maxLogLuminance - minLogLuminance;

        float pixelCount = globalUniform.renderWidth * globalUniform.renderHeight;
        float blackPixelCount = countInLocalBin;

        uint finalWeightedCount = histogramWorkGroup[0];
        
        float weightedLogAverage = (finalWeightedCount / max(pixelCount - blackPixelCount, 1.0)) - 1.0;
        float weightedAvgLuminance = exp2(weightedLogAverage / 254.0 * logLuminanceRange + minLogLuminance);

        float lastFrameLuminance = tonemapping.avgLuminance;

        float tau = 1.1;
        float t = 1.0 - exp(-globalUniform.timeDelta * tau);
        float adaptedLuminance = lastFrameLuminance + (weightedAvgLuminance - lastFrameLuminance) * t;

        tonemapping.avgLuminance = adaptedLuminance;
    }
}