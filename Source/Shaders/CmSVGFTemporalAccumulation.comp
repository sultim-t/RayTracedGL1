// Copyright (c) 2021 Sultim Tsyrendashiev
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.
// 
// Copyright (c) 2018, Christoph Schied
// All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//     * Redistributions of source code must retain the above copyright
//       notice, this list of conditions and the following disclaimer.
//     * Redistributions in binary form must reproduce the above copyright
//       notice, this list of conditions and the following disclaimer in the
//       documentation and/or other materials provided with the distribution.
//     * Neither the name of the Karlsruhe Institute of Technology nor the
//       names of its contributors may be used to endorse or promote products
//       derived from this software without specific prior written permission.
// 
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
// ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
// WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY
// DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
// (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
// LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
// ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
// SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

#version 460

// "Spatiotemporal Variance-Guided Filtering: Real-Time Reconstruction for Path-Traced Global Illumination", C.Schied et al.
// 4.1 Temporal Filtering

#define DESC_SET_FRAMEBUFFERS 0
#define DESC_SET_GLOBAL_UNIFORM 1
#include "ShaderCommonGLSLFunc.h"

layout(local_size_x = COMPUTE_SVGF_TEMPORAL_GROUP_SIZE_X, local_size_y = COMPUTE_SVGF_TEMPORAL_GROUP_SIZE_X, local_size_z = 1) in;

// Ray Tracing Gems II. Chapter 49: ReBLUR: A Hierarchical Recurrent Denoiser
float getParallax(vec3 pos, vec3 posPrev)
{
    vec3 cameraDelta = globalUniform.cameraPositionPrev.xyz - globalUniform.cameraPosition.xyz;

    vec3 v = normalize(pos);
    vec3 vPrev = normalize(posPrev - cameraDelta);
    float cosa = saturate(dot(v, vPrev));
    float parallax = sqrt(1.0 - cosa * cosa) / max(cosa, 1e-6);
    parallax *= 60; // optionally normalized to 60 FPS

    return parallax;
}
#define SPEC_MAX_HITDISTANCE_ACCUM_FRAMES 16
#define SPEC_MIN_ACCUM_FRAMES 0
#define SPEC_MAX_ACCUM_FRAMES 64
#define SPEC_ACCUM_BASE_POWER 0.5   // [0.5, 1] greater values - less aggressive accumulation
#define SPEC_ACCUM_CURVE      0.66  // [1 low, 0.5 high] aggressiveness of history rejection depending on viewing angle
float getSpecAccumSpeed(float Amax, float roughness, float NoV, float parallax)
{
    float acos01sq = 1.0 - NoV; // Approximation of acos^2 in normalized form
    float a = pow(saturate(acos01sq), SPEC_ACCUM_CURVE);
    float b = 1.1 + roughness * roughness;
    float parallaxSensitivity = (b + a) / (b - a);
    float powerScale = 1.0 + parallax * parallaxSensitivity;
    float f = 1.0 - exp2(-200.0 * roughness * roughness);
    f *= pow(saturate(roughness), SPEC_ACCUM_BASE_POWER * powerScale);
    float A = SPEC_MAX_ACCUM_FRAMES * f;

    return clamp(A, SPEC_MIN_ACCUM_FRAMES, Amax);
}
float getSpecularDominantFactor(float nv, float roughness)
{
    float a = 0.298475 * log(39.4115 - 39.0029 * roughness);
    float f = pow(clamp(1.0 - nv, 0.0, 1.0), 10.8649)*(1.0 - a) + a;

    return clamp(f, 0.0, 1.0);
}

void main()
{
    const ivec2 pix = ivec2(gl_GlobalInvocationID);

    if (pix.x >= uint(globalUniform.renderWidth) || pix.y >= uint(globalUniform.renderHeight))
    {
        return;
    }

    const vec3 unfilteredDiff   = texelFetchUnfilteredDirect(       pix);
    const SH unfilteredIndirSH  = texelFetchUnfilteredIndirectSH(   pix);
    const vec3 unfilteredSpec   = texelFetchUnfilteredSpecular(     pix);
    // xyz - direction, w - hit distance
    const vec4 unfilteredRayInfo= texelFetch(framebufViewDirection_Sampler, pix, 0);

    const ivec3 chRenderArea    = getCheckerboardedRenderArea(      pix);
    const float motionZ         = texelFetch(framebufMotion_Sampler,pix, 0).z;
    const float depth           = texelFetch(framebufDepth_Sampler, pix, 0).r;
    const vec3 normal           = texelFetchNormal(                 pix);
    const float roughness       = texelFetch(framebufMetallicRoughness_Sampler, pix, 0).g;

    const float l = getLuminance(unfilteredDiff);
    const vec2 moments = vec2(l, l * l);


    ivec2 pixPrev; vec2 subPix;
    {
        const vec2 posPrev = getPrevScreenPos(framebufMotion_Sampler, pix);

        pixPrev = ivec2(floor(posPrev - 0.5));
        subPix = (posPrev - 0.5) - floor(posPrev - 0.5);
    }
    const float bilinearWeights[2][2] = 
    {
        { (1.0 - subPix.x) * (1.0 - subPix.y), subPix.x * (1.0 - subPix.y) },
        { (1.0 - subPix.x) * subPix.y,         subPix.x * subPix.y         },
    };


    // history length for direct, indirect, specular, hit distance for specular
    vec4 historyLengthDIS = vec4(0.0);
    vec3 diffPrev = vec3(0.0);
    vec2 momentsPrev = vec2(0.0);
    vec3 specPrevOnSurf = vec3(0.0);
    SH indirPrev = newSH();
    float weightSum = 0.0;

    // 2x2 tap bilinear interpolation
    for (int yy = 0; yy <= 1; yy++)
    {
        for(int xx = 0; xx <= 1; xx++)
        {
            {
                ivec2 xy = pixPrev + ivec2(xx, yy);

                float depthPrev = texelFetch(framebufDepth_Prev_Sampler, xy, 0).r;
                vec3 normalPrev = texelFetchNormal_Prev(xy);

                bool isConsistent = 
                    testPixInRenderArea(xy, chRenderArea) &&
                    testReprojectedDepth(depth, depthPrev, motionZ) &&
                    testReprojectedNormal(normal, normalPrev);

                if (isConsistent)
                {
                    const float weight = bilinearWeights[yy][xx];

                    diffPrev            += weight * texelFetch(framebufDiffColorHistory_Sampler,        xy, 0).rgb;
                    momentsPrev         += weight * texelFetch(framebufDiffAccumMoments_Prev_Sampler,   xy, 0).rg;    
                    historyLengthDIS    += weight * texelFetch(framebufAccumHistoryLength_Prev_Sampler, xy, 0);
                    specPrevOnSurf      += weight * texelFetchSpecAccumColor_Prev(xy);
                    accumulateSH(indirPrev, texelFetchIndirAccumSH_Prev(xy), weight);

                    weightSum += weight;
                }
            }
        }
    }
    

    float antilagAlpha_Diff, antilagAlpha_Indir, antilagAlpha_Spec;

#if GRADIENT_ESTIMATION_ENABLED
    {
        const vec3 gradDIS = texelFetch(framebufDISPingGradient_Sampler, pix / COMPUTE_ASVGF_STRATA_SIZE, 0).xyz;
        antilagAlpha_Diff  = gradDIS[0];
        antilagAlpha_Indir = gradDIS[1];
        antilagAlpha_Spec  = gradDIS[2];
    }
#else
    antilagAlpha_Diff  = 0.0;
    antilagAlpha_Indir = 0.0;
    antilagAlpha_Spec  = 0.0;
#endif


    vec3 specColorAccum;
    float specHistoryLengthAccum;
    float specHitDistanceAccum;

    // found consistent info
    if (weightSum > 0.0001)
    {
        specPrevOnSurf /= weightSum;
        const float specHistoryLength   = historyLengthDIS[2] / weightSum;
        const float specHitDistancePrev = historyLengthDIS[3] / weightSum;

        const vec3 surfPos = texelFetch(framebufSurfacePosition_Sampler, pix, 0).xyz;
        const vec3 surfPosPrev = texelFetch(framebufSurfacePosition_Prev_Sampler, pixPrev, 0).xyz;
        float nv = max(0, dot(normal, -unfilteredRayInfo.xyz));

        const float A = clamp(
            specHistoryLength * pow(1.0 - antilagAlpha_Spec, 5), 
            SPEC_MIN_ACCUM_FRAMES, 
            SPEC_MAX_ACCUM_FRAMES);

        const float parallax = getParallax(surfPos, surfPosPrev);
        float AOnSurf = getSpecAccumSpeed(A, roughness, nv, parallax);

        const vec3 specOnSurf = mix(specPrevOnSurf, unfilteredSpec, 1.0 / (1.0 + AOnSurf));
        const float hitDistOnSurf = mix(specHitDistancePrev, unfilteredRayInfo.w, 1.0 / (1.0 + AOnSurf));

        {
            specColorAccum = specOnSurf;
            specHistoryLengthAccum = AOnSurf;
            specHitDistanceAccum = hitDistOnSurf;
        }
        const vec3 virtualPos = surfPos + unfilteredRayInfo.xyz * hitDistOnSurf * getSpecularDominantFactor(nv, roughness);

        // specular
        {
            ivec2 pixPrev_Spec;
            vec2 subPix_Spec;
            {
                const vec4 viewSpacePosCur   = globalUniform.view     * vec4(virtualPos, 1.0);
                const vec4 viewSpacePosPrev  = globalUniform.viewPrev * vec4(virtualPos, 1.0);
                const vec4 clipSpacePosCur   = globalUniform.projection     * viewSpacePosCur;
                const vec4 clipSpacePosPrev  = globalUniform.projectionPrev * viewSpacePosPrev;
                const vec3 ndcCur            = clipSpacePosCur.xyz  / clipSpacePosCur.w;
                const vec3 ndcPrev           = clipSpacePosPrev.xyz / clipSpacePosPrev.w;
                const vec2 screenSpaceCur    = ndcCur.xy  * 0.5 + 0.5;
                const vec2 screenSpacePrev   = ndcPrev.xy * 0.5 + 0.5;
                const vec2 specMotion = (screenSpacePrev - screenSpaceCur);

                const vec2 specPosPrev = getPrevScreenPos(specMotion, pix);

                pixPrev_Spec = ivec2(floor(specPosPrev - 0.5));
                subPix_Spec = (specPosPrev - 0.5) - floor(specPosPrev - 0.5);
            }
            const float bilinearWeights_Spec[2][2] = 
            {
                { (1.0 - subPix_Spec.x) * (1.0 - subPix_Spec.y), subPix_Spec.x * (1.0 - subPix_Spec.y) },
                { (1.0 - subPix_Spec.x) * subPix_Spec.y,         subPix_Spec.x * subPix_Spec.y         },
            };
            
            vec3 specPrevVirtual = vec3(0.0);
            float weightSumVirtual = 0.0;
            for (int yy = 0; yy <= 1; yy++)
            {
                for(int xx = 0; xx <= 1; xx++)
                {
                    ivec2 xy_Spec = pixPrev_Spec + ivec2(xx, yy);

                    float depthPrev = texelFetch(framebufDepth_Prev_Sampler, xy_Spec, 0).r;
                    vec3 normalPrev = texelFetchNormal_Prev(xy_Spec);

                    bool isConsistent = 
                        testPixInRenderArea(xy_Spec, chRenderArea) &&
                        testReprojectedDepth(depth, depthPrev, motionZ) &&
                        testReprojectedNormal(normal, normalPrev);

                    if (isConsistent)
                    {
                        const float weight = bilinearWeights_Spec[yy][xx];

                        specPrevVirtual  += weight * texelFetchSpecAccumColor_Prev(xy_Spec);
                        weightSumVirtual += weight;
                    }
                }
            }

            if (weightSumVirtual > 0.0001)
            {
                specPrevVirtual /= weightSumVirtual;

                float Avirt = getSpecAccumSpeed(A, roughness, nv, 0);

                // TODO
                float confidence = 1;

                #define MIP_NUM 3
                float Amin = min(Avirt, MIP_NUM * sqrt(roughness));
                float a = mix(1.0 / (1.0 + Amin), 1.0 / (1.0 + Avirt), confidence);

                Avirt = 1.0 / a - 1.0;
                float Ahitdist = min(Avirt, SPEC_MAX_HITDISTANCE_ACCUM_FRAMES);

                vec3 specVirt = mix(specPrevOnSurf, unfilteredSpec, 1.0 / (1.0 + Avirt));
                
                float amount = getSpecularDominantFactor(nv, roughness);
                a = mix(1.0 / (1.0 + AOnSurf), 1.0 / (1.0 + Avirt), amount);
                {
                    specColorAccum = mix(specOnSurf, specVirt, amount);
                    specHistoryLengthAccum = 1.0 / a - 1.0;
                    specHitDistanceAccum = mix(specHitDistancePrev, unfilteredRayInfo.w, 1.0 / (1.0 + Ahitdist));
                }
            }
        }
    }
    else
    {
        specColorAccum = unfilteredSpec;
        specHistoryLengthAccum = 1.0;
        specHitDistanceAccum = unfilteredRayInfo.w;
    }


    vec3 diffColorAccum;
    vec2 diffMomentsAccum;
    float diffHistoryLengthAccum;

    SH indirSHAccum;
    float indirHistoryLengthAccum;

    // found consistent info
    if (weightSum > 0.0001)
    {
        const float invWeightSum = 1.0 / weightSum;

        diffPrev          *= invWeightSum;
        momentsPrev       *= invWeightSum;
        multiplySH(indirPrev, invWeightSum);

        float diffHistoryLength  = historyLengthDIS[0] * invWeightSum;
        float indirHistoryLength = historyLengthDIS[1] * invWeightSum;
        
        // direct diffuse
        {
            diffHistoryLength *= pow(1.0 - antilagAlpha_Diff, 10);
            diffHistoryLength = clamp(diffHistoryLength + 1.0, 1.0, 256.0);

            const float minAlpha = 0.01;
            float alphaColor   = max(minAlpha, 1.0 / diffHistoryLength);
            float alphaMoments = max(minAlpha, 1.0 / diffHistoryLength);

            alphaColor   = mix(alphaColor,   1.0, antilagAlpha_Diff);
            alphaMoments = mix(alphaMoments, 1.0, antilagAlpha_Diff);

            diffColorAccum   = mix(diffPrev, unfilteredDiff, alphaColor);
            diffMomentsAccum = mix(momentsPrev, moments, alphaMoments);
            diffHistoryLengthAccum = diffHistoryLength;
        }

        // indirect diffuse
        {
            antilagAlpha_Indir = 0.0;

            indirHistoryLength *= pow(1.0 - antilagAlpha_Indir, 10);
            indirHistoryLength = clamp(indirHistoryLength + 1.0, 1.0, 256.0);

            const float minAlpha = 0.01;
            float alphaColor = max(minAlpha, 1.0 / indirHistoryLength);

            alphaColor = mix(alphaColor, 1.0, antilagAlpha_Indir);

            indirSHAccum = mixSH(indirPrev, unfilteredIndirSH, alphaColor);
            indirHistoryLengthAccum = indirHistoryLength;
        }
    }
    else
    {
        diffColorAccum = unfilteredDiff;
        diffMomentsAccum = moments;
        diffHistoryLengthAccum = 1.0;

        indirSHAccum = unfilteredIndirSH;
        indirHistoryLengthAccum = 1.0;
    }

    imageStoreDiffAccumColor(               pix, diffColorAccum);
    imageStore(framebufDiffAccumMoments,    pix, vec4(diffMomentsAccum, 0.0, 0.0));
    imageStore(framebufAccumHistoryLength,  pix, vec4(diffHistoryLengthAccum, indirHistoryLengthAccum, specHistoryLengthAccum, specHitDistanceAccum));

    imageStoreSpecAccumColor(               pix, specColorAccum);
    imageStoreSpecPingColor(                pix, specColorAccum);

    imageStoreIndirAccumSH(                 pix, indirSHAccum);
    imageStoreIndirPingSH(                  pix, indirSHAccum);
}