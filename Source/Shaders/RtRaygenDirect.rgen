// Copyright (c) 2021 Sultim Tsyrendashiev
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#version 460

// don't need albedo for direct illumination
#define MATERIAL_MAX_ALBEDO_LAYERS 0

#define DESC_SET_TLAS 0
#define DESC_SET_FRAMEBUFFERS 1
#define DESC_SET_GLOBAL_UNIFORM 2
#define DESC_SET_VERTEX_DATA 3
#define DESC_SET_TEXTURES 4
#define DESC_SET_RANDOM 5
#define DESC_SET_LIGHT_SOURCES 6
#define RAYGEN_SHADOW_PAYLOAD
#include "RaygenCommon.h"

void main()
{
    const ivec2 pix = ivec2(gl_LaunchIDEXT.xy);

    const vec4 albedo4 = texelFetchAlbedo(pix);

    if (isSky(albedo4))
    {
        imageStore(framebufUnfilteredDirect, pix, uvec4(0));
        imageStore(framebufUnfilteredSpecular, pix, uvec4(0));
        return;
    }

    const uint seed = getCurrentRandomSeed(pix);
    
    const vec4 surfPosition           = texelFetch(framebufSurfacePosition_Sampler, pix, 0);
    const vec3 surfNormal             = texelFetchNormal(pix);
    const vec3 surfNormalGeom         = texelFetchNormalGeometry(pix);
    const vec4 surfMetallicRoughness  = texelFetch(framebufMetallicRoughness_Sampler, pix, 0);
    const vec3 viewDirection          = texelFetch(framebufViewDirection_Sampler, pix, 0).xyz;
    const uint grFB                   = texelFetch(framebufGradientSamples_Sampler, pix / COMPUTE_ASVGF_STRATA_SIZE, 0).x;
    const uint sectorArrayIndex       = texelFetch(framebufSectorIndex_Sampler, pix, 0).r;

    const float surfRoughness         = surfMetallicRoughness.g;
    const float surfMetallic          = surfMetallicRoughness.r;
    const vec3 surfSpecularColor      = getSpecularColor(albedo4.rgb, surfMetallic);

    const uint instCustomIndex        = floatBitsToUint(surfPosition.a);

    const bool isGradientSample       = (pix.x % COMPUTE_ASVGF_STRATA_SIZE) == (grFB % COMPUTE_ASVGF_STRATA_SIZE) &&
                                        (pix.y % COMPUTE_ASVGF_STRATA_SIZE) == (grFB / COMPUTE_ASVGF_STRATA_SIZE);
    
    vec3 diffuse, specular;
    processDirectIllumination(
        seed, 
        instCustomIndex, surfPosition.xyz, surfNormal, surfNormalGeom, surfRoughness, surfSpecularColor, sectorArrayIndex,
        -viewDirection,
        isGradientSample, 
        0,
        diffuse, specular);

    imageStoreUnfilteredDirect(pix, diffuse);
    imageStoreUnfilteredSpecular(pix, specular);
}